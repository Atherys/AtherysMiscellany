buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'org.spongepowered.plugin' version '0.9.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

dependencies {
    compile 'org.spongepowered:spongeapi:7.1.0'
    compile 'org.spongepowered:mixin:0.8.3-SNAPSHOT'
    compile 'com.atherys:AtherysCore:2.0.0'
    compile 'com.atherys:AtherysRPG:2.2.2'
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://repo.atherys.com' }
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    mappings = "stable_39"
    makeObfSourceJar = false
}

mixin {
    add sourceSets.main, "mixins.atherysmiscellany.refmap.json"
}

jar {
    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.atherysmiscellany.json',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true',
    )
}

version = "1.0.0"
group = "com.atherys.miscellany"
archivesBaseName = "AtherysMiscellany"

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
